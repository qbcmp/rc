#!/bin/zsh

setopt prompt_subst

autoload -Uz vcs_info
zstyle ':vcs_info:git:*' formats '(%b) '
zstyle ':vcs_info:git:*' actionformats '(%b|%a) '

_k8s_prompt_info=""

precmd() {
  vcs_info
  _k8s_prompt_info=$(get_k8s_context)
}

get_k8s_context() {
  local k8s_info=$(kubectl config view --minify -o "jsonpath={.current-context}{'\t'}{.contexts[?(@.name==\"{.current-context}\")].context.namespace}" 2>/dev/null)
  if [[ -n "$k8s_info" ]]; then
    local k8s_context=$(echo "$k8s_info" | cut -f1)
    local k8s_namespace=$(echo "$k8s_info" | cut -f2)
    k8s_namespace=${k8s_namespace:-default}
    echo "%F{cyan}${k8s_context}:${k8s_namespace}%f"
  fi
}

PROMPT_DEFAULT='%F{8}%m %F{yellow}%1~ %F{green}${vcs_info_msg_0_}%F{white}$ %f'
PROMPT_K8S='%F{8}%m %F{yellow}%1~ %F{green}${vcs_info_msg_0_}${_k8s_prompt_info} %F{white}$ %f'
PROMPT="$PROMPT_DEFAULT"

toggle_prompt_style() {
  if [[ "$PROMPT" == "$PROMPT_DEFAULT" ]]; then
    PROMPT="$PROMPT_K8S"
  else
    PROMPT="$PROMPT_DEFAULT"
  fi
  zle reset-prompt
}

zle -N toggle_prompt_style

bindkey '^u' toggle_prompt_style # Ctrl + u toggles the left prompt

RPROMPT='${_k8s_rprompt_visible:+"${_k8s_prompt_info}"}'

toggle_rprompt_style() {
  if [[ -n "${_k8s_rprompt_visible}" ]]; then
    unset _k8s_rprompt_visible
  else
    _k8s_rprompt_visible=1
  fi
  zle reset-prompt
}

zle -N toggle_rprompt_style
bindkey '^b' toggle_rprompt_style # Ctrl + b toggles the right prompt